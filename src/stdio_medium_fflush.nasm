;
; based on .nasm source file generated by soptcc.pl from c_stdio_medium_fflush.c
; Compile to i386 ELF .o object: nasm -O999999999 -w+orphan-labels -f elf -o c_stdio_medium_fflush.o c_stdio_medium_fflush.nasm
;
; Uses: CONFIG_PIC
; Uses: CONFIG_FLUSH_INLINE_DISCARD_BUF
;

bits 32
cpu 386

global mini_fflush
global mini_fflush_RP3
%ifdef CONFIG_SECTIONS_DEFINED
%elifidn __OUTPUT_FORMAT__, bin
section .text align=1
section .rodata align=1
section .data align=1
section .bss align=1
mini_write equ +0x12345678
mini___M_discard_buf_RP3 equ +0x12345679
%else
extern mini_write
extern mini___M_discard_buf_RP3
section .text align=1
section .rodata align=1
section .data align=1
section .bss align=1
%endif

section .text

mini_fflush:  ; int mini_fflush(FILE *filep);
		mov eax, [esp+1*4]  ; filep.
		; Falls through to mini_fflush_RP3.

mini_fflush_RP3:  ; int mini_fflush_RP3(FILE *filep) __attribute__((__regparm__(3)));
		push esi  ; Save.
		push ebx  ; Save.
		xchg ebx, eax  ; EBX := EAX (filep); EAX := junk.
		or ecx, byte -1  ; EAX := -1. For the early `jbe short .return_ecx' below.
		cmp byte [ebx+0x14], 0x3
		jbe short .return_ecx
		mov esi, [ebx+0x18]
.more_bytes:	mov eax, [ebx]
		xor ecx, ecx  ; ECX := 0. Return value for `je short .flush_and_return_ecx' below.
		cmp eax, esi
		je short .flush_and_return_ecx
		sub eax, esi
		push eax
		push esi
		push dword [ebx+0x10]
		call mini_write  ; Keeps EBX intact because of __cdecl.
		add esp, byte 3*4  ; Clean up stack of mini_write.
		add esi, eax
		inc eax
		cmp eax, byte 2  ; Sets CF := (EAX < 2).
		dec eax  ; Doesn't affect CF.
		jnc short .more_bytes
.sub_flush_and_return_m1:
		sbb ecx, ecx  ; ECX := -1 because CF == 1. Return value.
		sub esi, eax
.flush_and_return_ecx:
		sub esi, [ebx+0x18]
		add [ebx+0x20], esi
%ifdef CONFIG_FLUSH_INLINE_DISCARD_BUF
; void mini___M_discard_buf(FILE *filep) {
;   filep->buf_read_ptr = filep->buf_write_ptr = filep->buf_last = filep->buf_start;
;   if (IS_FD_ANY_READ(filep->dire)) filep->buf_write_ptr = filep->buf_end;  /* Sentinel. */
; }
		mov edx, [ebx+0x18]  ; EBX == filep.
		mov [ebx+0xc], edx
		mov [ebx], edx
		mov [ebx+0x8], edx
		mov al, [ebx+0x14]
		dec eax  ; AL -= 1; higher bits of EAX := junk.
		cmp al, 0x2
		ja short .flushed
		mov edx, [ebx+0x4]
		mov [ebx], edx
.flushed:
%else
		xchg eax, ebx  ; EAX := EBX (filep); EBX := junk.
		call mini___M_discard_buf_RP3  ; Keeps EBX and (because of __cdecl) ECX intact. Ruins EDX.
%endif
.return_ecx:	xchg eax, ecx  ; EAX := ECX (return value); ECX := junk.
		pop ebx  ; Restore.
		pop esi  ; Restore.
		ret

%ifdef CONFIG_PIC  ; Already position-independent code.
%endif

; __END__
